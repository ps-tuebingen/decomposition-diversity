data Identifier where
     Tik()
     Tok(Identifier)

data Expr where
    EVar(Identifier)
    ENot(Expr)
    EAnd(Expr, Expr)
    EOr(Expr, Expr)

data Value where
     ValPosVar(Identifier)
     ValNegVar(Identifier)
     ValAnd(Value, Value)
     ValOr(Value, Value)

data Redex where
    RedNot(Expr)
    RedAnd(Expr, Expr)
    RedOr(Expr, Expr)

data Found where
    FoundValue(Value)
    FoundRedex(Redex)

codata Value2Found where
       Apply(Value) : Found

consumer function Expr:SearchNeg(ctx : Value2Found) : Found :=
	 case EVar(n)     => ctx.Apply(ValNegVar(n))
	 case ENot(e)     => FoundRedex(RedNot(e))
	 case EAnd(e1,e2) => FoundRedex(RedAnd(e1,e2))
	 case EOr(e1,e2)  => FoundRedex(RedOr(e1,e2))

consumer function Expr:SearchPos(ctx : Value2Found) : Found :=
	 case EVar(n)     => ctx.Apply(ValPosVar(n))
	 case ENot(e)     => e.SearchNeg(ctx)
	 case EAnd(e1,e2) => e1.SearchPos( comatch Value2Found:AndCtx1 using e2 := e2 : Expr, ctx := ctx : Value2Found  with
                                                    cocase Apply(val1) => e2.SearchPos( comatch Value2Found:AndCtx2 using val1 := val1 : Value , ctx := ctx : Value2Found with
                                                                                             cocase Apply(val2) => ctx.Apply(ValAnd(val1, val2))
                                                                                       )
                                          )
	 case EOr(e1,e2)  => e1.SearchPos( comatch Value2Found:OrCtx1 using e2 := e2 : Expr, ctx := ctx : Value2Found  with
                                                    cocase Apply(val1) => e2.SearchPos( comatch Value2Found:OrCtx4 using val1 := val1 : Value , ctx := ctx : Value2Found with
                                                                                             cocase Apply(val2) => ctx.Apply(ValOr(val1, val2))
                                                                                       )
                                          )

function search(e : Expr) : Found :=
  e.SearchPos(comatch Value2Found:EmptyCtx with
                    cocase Apply(val) => FoundValue(val)
  )

